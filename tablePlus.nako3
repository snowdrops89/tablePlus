＃変数
//変数 昇順＝はい。
//変数 選択行色＝「#CCF」
//変数 前クリックセル＝空。

＃テーブル＋
●(テーブルに)テーブル拡張設定
    テーブルの「昇順」に、はいをデータ属性設定。
    テーブル背景色＝DOM部品オプション.テーブル背景色。
    選択行色＝テーブル背景色[3]。
    もし、選択行色＝未定義ならば、選択行色＝「#CCCCFF」
    テーブルの「選択行色」に選択行色をデータ属性設定。
    テーブル背景色＝テーブル背景色をJSONエンコード。
    テーブルの「背景色」にテーブル背景色をデータ属性設定。
    テーブルの「前クリックセル」に空をDOM属性設定。
    データ＝空配列。
    全行＝テーブルの「tr」をDOM子要素全取得。
    全行を反復:
        行＝対象キー。
        もし、行＝０ならば、:
            行セル＝対象の「th」をDOM子要素全取得。
        違えば、:
            行セル＝対象の「td」をDOM子要素全取得。
        データ[行]＝空配列。
        行セルを反復:
            列＝対象キー。
            データ[行][列]＝対象.textContent。
    テーブルにデータをテーブルデータ設定。
ここまで。

●(テーブルの)テーブルアイテム取得
    データ＝テーブルの「データ」をデータ属性取得。
    データ＝データをJSONデコード
    アイテム＝データの０を表列削除。
    アイテムを戻す。
ここまで。

●(テーブルにアイテムを)テーブルデータ設定
    連番＝０から（アイテムの要素数-1）までの配列連番作成。
    データ＝アイテムの０に連番を表列挿入。
    テーブルの「データ」にデータをJSONエンコードをデータ属性設定。
ここまで。

＃編集可能
●(テーブルの)テーブル編集可能
    変数 全セル＝テーブルの「td」をDOM子要素全取得。
    全セルを反復。テーブルの対象に編集可能セル設定。。。

    # セル内の改行入力を禁止する。
    テーブルのキー押した時には、:
        もし、押されたキー＝「Enter」ならば、:
            対象イベントのDOMイベント処理停止。
ここまで。

●(テーブルに)テーブル行追加
    元テーブル背景色＝DOM部品オプション.テーブル背景色。
    DOM部品オプション.テーブル背景色＝テーブルの「背景色」をデータ属性取得して、JSONデコード。
    アイテム＝テーブルのテーブルアイテム取得。
    空行＝空配列。追加行＝アイテムの表行数。
    アイテムの表列数回。空行に空を配列追加。。。
    テーブルの[アイテムの表行数,0]を[空行]にテーブルセル変更。
    # 追加された行の全セルに編集可能設定を付ける
    アイテム[追加行]＝空配列。
    アイテムの表列数回
        アイテム[追加行][回数-1]＝空。
        テーブルから[追加行,回数-1]のテーブルセル取得。
        テーブルのそれに編集可能セル設定。
    ここまで。
    テーブルにアイテムをテーブルデータ設定。
    DOM部品オプション.テーブル背景色＝元テーブル背景色。
ここまで。

●(テーブルのセルに)編集可能セル設定
    # 最初のセルからセルの高さを取得
    WINDOWの「getComputedStyle」を(テーブルから[1,0]のテーブルセル取得)でJSメソッド実行。
    セル高さ＝それ.height。//セル高さを表示。
    セルの「高さ」にセル高さをDOMスタイル設定。
    セル.contentEditable＝true。# 編集可能にする
    セルの「blur」がDOMイベント発火した時には、:
        もし、対象.nodeName＝「TD」ならば、:
            # 改行を含むコピペ対策
            テキスト＝対象.textContent。
            対象にテキストをテキスト設定。
            # テーブル数値右寄せ
            もし、DOM部品オプション.「テーブル数値右寄せ」＝trueならば、
                テキストを数列か判定。
                もし、それがtrueならば、
                    対象の「行揃え」に「右」をDOMスタイル設定。
                違えば、
                    対象の「行揃え」に「左」をDOMスタイル設定。
                ここまで。
            ここまで。
            # セルの内容をデータに反映
            行＝対象のセル行番号。
            列＝対象のセル列番号。
            テーブル＝対象.parentNode.parentNode。
            アイテムはテーブルのテーブルアイテム取得。
            もし、(アイテムの表行数)≦行ならば、アイテム[行]＝空配列。
            アイテム[行][列]＝テキスト。
            テーブルにアイテムをテーブルデータ設定。
ここまで。

＃行選択
●(テーブルの)テーブル行選択可能
    テーブルをマウス押した時には、:
        もし、対象.nodeName＝「TD」ならば、:
            変数 クリック行＝対象.parentNode。
            変数 テーブル＝クリック行.parentNode。
            変数 行色情報＝テーブルの「背景色」をデータ属性取得して、JSONデコード。
            変数 選択行色＝テーブルの「選択行色」をデータ属性取得。
            変数 前クリックセル＝テーブルの「前クリックセル」をDOM属性取得。
            前クリックセルをコンソール表示
            もし、前クリックセルが空でなければ、:
                変数 前行数＝前クリックセルのセル行番号。
                変数 前クリック行＝前クリックセル.parentNode。
                変数 前背景色＝行色情報[前行数%2+1]。
                前クリック行の「背景色」に前背景色をDOMスタイル設定。
            クリック行の「背景色」に選択行色をDOMスタイル設定。
            テーブル.前クリックセル＝対象。

ここまで。

＃セル幅変更
●(テーブルの)テーブルセル幅変更可能
    # ヘッダの設定
    変数 全ヘッダ＝テーブルの「th」をDOM子要素全取得。
    母艦＝DOM親要素。
    全ヘッダを反復:
        # ヘッダの余白サイズを調べ、ヘッダの余白は0にする。
        WINDOWの「getComputedStyle」を対象でJSメソッド実行。
        余白サイズ＝それ.padding。//.余白 //できない
        最小幅＝それ.width。
        対象.余白＝0。
        # ヘッダのテキストを新しく作ったdivの中にうつす
        テキスト＝対象のテキスト取得。
        対象に空をテキスト設定。
        対象にDOM親部品設定。
        「div」のDOM部品作成。
        それにテキストをテキスト設定。
        # リサイズの設定。horizontalが横幅。
        # overflowがvisibleになっているとresizeが効かない
        # min-widthでヘッダの幅より小さくならないようにする。
        # ヘッダの余白をうつす
        それに{
            "resize":"horizontal",
            "overflow":"hidden",
            "white-space":"nowrap",
            "min-width":最小幅,
            "余白":余白サイズ
        }をDOMスタイル一括設定。
    母艦にDOM親部品設定。
ここまで。

＃自動ソート

●(テーブルの)テーブル自動ソート可能
    変数 全ヘッダ＝テーブルの「th」をDOM子要素全取得。
    母艦＝DOM親要素。
    全ヘッダを反復:
        記号表示場所＝対象。
        もし、対象.firstChild.tagName＝「DIV」ならば、記号表示場所＝対象.firstChild。
        記号表示場所にDOM親部品設定。
        空のラベル作成。
        それに{
            "margin-left":"0.5em",
            "vertical-align":"middle",
            "font-size":"0.7em",
            "色":灰色
        }をDOMスタイル一括設定。
    母艦にDOM親部品設定。
    テーブルをクリックした時には、
        # 三角のラベルをクリックした時も動くようにする
        もし、(対象.nodeName＝「TH」)または(対象.nodeName＝「SPAN」)または(対象.nodeName＝「DIV」)ならば、:
            セル＝対象。
            もし、(対象.nodeName＝「SPAN」)または(対象.nodeName＝「DIV」)ならば、セル＝対象.parentNode。
            テーブル＝セル.parentNode.parentNode。
            アイテム＝テーブルのテーブルアイテム取得。
            変数 昇順＝テーブルの「昇順」をデータ属性取得。
            ソート列＝セルのセル列番号。
            ヘッダ＝アイテムの0を配列切り取り。
            アイテムのソート列を表ソート。
            もし、昇順＝はいでなければ、アイテムを配列逆順。
            テーブルの[1,0]をアイテムにテーブルセル変更。
            アイテムの0へヘッダを配列挿入。
            記号＝「▼」
            もし、昇順=はいならば、記号＝「▲」
            昇順=昇順*-1。
            テーブルの「昇順」に昇順をデータ属性設定。            
            全記号欄＝テーブルの「SPAN」をDOM子要素全取得。
            全記号欄を反復
                もし、対象キー＝ソート列ならば、
                    対象に記号をテキスト設定。
                違えば、
                    対象に空をテキスト設定。
                ここまで。
            ここまで。
            テーブルにアイテムをテーブルデータ設定。
    ここまで。
ここまで。


＃
●(セルの)セル行番号
    セル.parentNode.rowIndexを戻す。
ここまで。

●(セルの)セル列番号
    セル.cellIndexを戻す。
ここまで。

●(テーブルから行列の)テーブルセル取得
    変数 [行,列]＝行列。
    テーブル["rows"][行]["cells"][列]を戻す。
ここまで。

●(テーブルから行の)テーブル行取得
    テーブル["rows"][行]を戻す。
ここまで。


もし、プラグイン名＝「メイン」ならば、
    # テーブルを作成
    テストデータ＝「名前,ふりがな,点数
    太郎,たろう,55
    花子,はなこ,100
    一郎,いちろう,80」をCSV取得。
    //アイテム＝テストデータを配列複製。

    DOM部品オプション.テーブル背景色＝["#AA4040","#ffffff","#fff0f0"]
    テストテーブル＝テストデータのテーブル作成。
    改行作成。

    確認ボタンは「データ確認」のボタン作成。
    確認ボタンをクリックした時には、
        テストテーブルのテーブルアイテム取得してJSONエンコードして表示。
    ここまで。

    追加ボタンは「一行追加」のボタン作成。
    追加ボタンをクリックした時には、
        テストテーブルにテーブル行追加
    ここまで。


    テストテーブルにテーブル拡張設定。
    テストテーブルのテーブル編集可能
    テストテーブルのテーブル行選択可能。
    テストテーブルのテーブルセル幅変更可能
    テストテーブルのテーブル自動ソート可能


    DOM部品オプション["テーブル背景色"] = [青色,緑色,赤色]
    テスト＝「名前,年齢
    桃太郎,98
    さる太郎,87
    きじ次郎,77」のテーブル作成。
    テストにテーブル拡張設定。
    テストのテーブル行選択可能。
ここまで。